// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  maxJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = "org.springframework"
    module = "spring-webflux"
    versions = "[5.0.0.RELEASE,)"
    assertInverse = true
  }
}

apply from: "${rootDir}/gradle/java.gradle"

sourceSets {
  main_java8 {
    java.srcDirs "${project.projectDir}/src/main/java8"
  }
}

compileMain_java8Java {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}
// Note: ideally lombok plugin would do this for us, but currently it doesn't support custom
// source sets. See https://github.com/franzbecker/gradle-lombok/issues/17.
dependencies {
  main_java8CompileOnly "org.projectlombok:lombok:${project.lombok.version}" transitive false
  main_java8AnnotationProcessor "org.projectlombok:lombok:${project.lombok.version}" transitive false
}

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}
compileTestJava {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

compileLatestDepTestJava {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

dependencies {
  // We use helpers from this project
  main_java8CompileOnly project(':dd-java-agent:instrumentation:reactor-core-3.1')
  main_java8CompileOnly group: 'org.springframework', name: 'spring-webflux', version: '5.0.0.RELEASE'

  main_java8Compile project(':dd-java-agent:agent-tooling')

  main_java8Compile deps.bytebuddy
  main_java8Compile deps.opentracing

  compileOnly sourceSets.main_java8.compileClasspath
  compile sourceSets.main_java8.output

  compile project(':dd-java-agent:agent-tooling')
  // We are using utils class from reactor-core instrumentation.
  // TODO: It is unclear why we need to use `compile` here (instead of 'compileOnly')
  compile project(':dd-java-agent:instrumentation:reactor-core-3.1')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  testCompile project(':dd-java-agent:instrumentation:java-concurrent')
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')
  testCompile project(':dd-java-agent:instrumentation:netty-4.1')
  testCompile project(':dd-java-agent:instrumentation:reactor-core-3.1')

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.0.0.RELEASE'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.0.RELEASE'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '2.0.0.RELEASE'
  testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4'

  // FIXME: Tests need to be updated to support 2.1+
  latestDepTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.0.+'
  latestDepTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.+'
  latestDepTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '2.0.+'
}
